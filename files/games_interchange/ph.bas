
100 REM PH -- PROGRAM TO DETERMINE PH VS. ENZYME ACTIVITY
110 REM COPYRIGHT 1973 - STATE UNIVERSITY OF NEW YORK
120 REM DEVELOPED BY J. FRIEDLAND
130 REM PROGRAMMED BY J. FRIEDLAND, L. KAUFMAN
135 REM REVISED BY B. ESRIG
140 REM LATEST REVISION: 10-2-73
150 DIM A(5),D(8)
160 FOR I=1 TO 7
170 READ D(I)
180 NEXT I
190 PRINT "DO YOU WISH INSTRUCTIONS (1=YES, 0=NO)"; 
200 INPUT Q
210 IF Q=0 THEN 250
220 LET N=0
230 IF Q<>1 THEN 190
235 PRINT
240 GOTO 300
250 PRINT "CODE LIST (1=YES,0=NO)";
260 INPUT N
270 IF N=0 THEN 615
280 IF N<>1 THEN 250
290 GOTO 440
300 PRINT "  THEORY HOLDS THAT THE BEHAVIOR OF ENZYMES IS IN PART" 
310 PRINT "A FUNCTION OF THE TYPES OF AMINO ACIDS AT THE ACTIVE SITE." 
320 PRINT "THIS PROGRAM ALLOWS YOU TO SET THE NUMBER AND TYPE OF"
330 PRINT "IONIZABLE AMINO ACID(S) PRESENT."
340 PRINT
350 PRINT "  THE COMPUTER WILL FIRST ASK:" 
360 PRINT "'HOW MANY IONIZABLE AMINO ACIDS AT THE ACTIVE SITE?'"   
370 PRINT "YOU CAN PICK ANY WHOLE NUMBER FROM 1 TO 4.  REAL ENZYMES" 
380 PRINT "ARE THOUGHT TO HAVE AT LEAST TWO AMINO ACIDS"
390 PRINT "PRESENT, BUT TRY DIFFERENT NUMBERS."
400 PRINT
410 PRINT"   THEN THE COMPUTER WILL ASK:"
420 PRINT" 'AMINO ACID 1  ---- CODE NO.?'"
430 PRINT "YOU CAN INPUT THE CODE NUMBER FOR ANY OF THE FOLLOWING ";
435 PRINT "AMINO ACIDS:"
440 PRINT "   CODE     NAME      R-GROUP OF AMINO ACID"
450 PRINT "   ----     ----      ---------------------"
460 PRINT "    1   ASPARTIC ACID   -CH2-COOH"
470 PRINT "    2   CYSTEINE        -CH2-SH"
480 PRINT "    3   GLUTAMIC ACID   -CH2-CH2-COOH"
490 PRINT "    4   TYROSINE        -CH2-C6H4-OH" 
500 PRINT "                                        NH2"
510 PRINT "                                        ^"
520 PRINT "    5   ARGININE        -CH2-CH2-CH2-NH-C=NH" 
530 PRINT "    6   HISTIDINE       -CH2-C3N2H3"
540 PRINT "    7   LYSINE          -CH2-CH2-CH2-CH2-NH2"
550 PRINT
560 IF N=1 THEN 615
570 PRINT "AMINO ACIDS 1-4 ARE CALLED ACIDIC AMINO ACIDS"
580 PRINT "AMINO ACIDS 5-7 ARE BASIC"
590 PRINT "BY TRYING ACIDIC AND BASIC AMINO ACIDS SEPARATELY AND IN" 
600 PRINT "COMBINATION YOU CAN DEVELOP TYPICAL PH PATTERNS ";
605 PRINT "FOR ENZYMES."
610 PRINT
615 PRINT
620 PRINT "HOW MANY IONIZABLE AMINO ACIDS AT ACTIVE SITE (UP TO 4)"; 
630 INPUT N
640 IF ABS(N-2.5)>1.5 THEN 620
650 IF N<>INT(N)THEN 620
660 PRINT
670 FOR T=1 TO N 
680 PRINT "AMINO ACID";T;" ---- CODE NO.";
690 INPUT B
700 IF INT(B)<>B THEN 680
710 IF ABS(B-4)>3 THEN 680
720 LET C=1
730 PRINT " SHOULD IT BE CHARGED (1=YES, 0=NO)";
740 INPUT Q
750 IF Q=1 THEN 780
760 IF Q<>0 THEN 730
770 LET C=-1
780 LET A(T)=C*D(B)
790 NEXT T 
800 PRINT
810 PRINT "PH RANGE FOR THIS EXPERIMENT:"
820 PRINT "  LOWER LIMIT"; 
830 INPUT P1 
840 IF ABS(P1-7)<=7 THEN 870
850 GOSUB 1210
860 GOTO 820
870 LET P1=INT(P1*2)/2
880 PRINT "  UPPER LIMIT"; 
890 INPUT P2 
900 IF P2<P1 THEN 880
910 IF P2<=14 THEN 940
920 GOSUB 1210
930 GOTO 880
940 LET P2=INT(P2*2+.999)/2
950 PRINT
960 PRINT "           RELATIVE ENZYME ACTIVITY"
970 PRINT"      0   10   20   30   40   50   60   70   80   90  100"
980 PRINT" PH   I----I----I----I----I----I----I----I----I----I----I"
990 FOR P=P1 TO P2 STEP .5
1000 LET Z=1
1010 FOR T= 1 TO N
1020 IF A(T)>0 THEN 1050
1030 LET L=P+A(T) 
1040 GOTO 1060
1050 LET L=A(T)-P 
1060 LET E=1/(10^L+1) 
1070 LET Z=Z*E
1080 NEXT T 
1090 IF P<>INT(P) THEN 1110
1100 PRINT P; 
1110 PRINT TAB(5);"I";TAB(INT(50*Z+6.5));"*"
1120 NEXT P 
1130 PRINT
1140 PRINT "ANOTHER EXPERIMENT (1=YES, 0=NO)";
1150 INPUT Q
1160 IF Q=1 THEN 610
1170 IF Q<>0 THEN 1140
1180 STOP 
1210 PRINT
1220 PRINT "PH RANGE IS LIMITED TO FROM 0 TO 14"
1230 PRINT
1240 RETURN 
1250 DATA 4.6,8.3,4.25,10.07,-1.48,-6.8,-10.0
1260 END
*U*';B$$BB$$BB$$BL