
100 REM SCATR1 - ALPHA PARTICLE SCATTERING EXPERIMENT SIMULATION
110 REM COPYRIGHT 1973 - STATE UNIVERSITY OF NEW YORK
120 REM DEVELOPED BY J. BRESMAN, A. CAGGIANO, AND D. SCARL
130 REM PROGRAMMED BY C. LOSIK AND D. SCARL,REVISED BY M. WEISNER
140 REM SCATER IS A SET OF ASSOCIATED PROGRAMS CONTAINING SCATR1,
150 REM SCATR2, AND SCATR3
160 REM LATEST REVISION: 12-12-73
170 RANDOMIZE
180 LET N0=0
190 DIM G(5),H(3),W(3),Z(3)
200 REM AVOGADRO'S NUMBER
210 LET V4=6.02E23
220 REM ELECTRON REST ENERGY (MEV)
230 LET B4=.511
240 REM CLASSICAL ELECTRON RADIUS (CM)
250 LET D4=2.8E-13
260 REM ALPHA PARTICLE CHARGE
270 LET Z4=2
280 REM ATOMIC NUMBER OF GOLD
290 LET Z(3)=79
300 REM ATOMIC WEIGHT OF GOLD
310 LET W(3)=197
320 REM DENSITY OF GOLD
330 LET H(3)=19.3
340 REM LOG OF NO. OF BEAM PARTICLES
350 LET B1=10
360 LET B=100
370 LET E4=5.3
380 LET T=1
390 LET T4=1E-5*T
400 LET A4=1
410 LET L4=10
420 PRINT "INSTRUCTIONS (0=NO,1=YES)";
430 INPUT I
440 IF I=0 THEN 580
450 IF I<>1 THEN 420
460 PRINT"A VERY RADIOACTIVE POLONIUM SOURCE PRODUCES A BEAM OF"
470 PRINT"100,000 ALPHA PARTICLES PER SECOND, EACH HAVING AN ENERGY"
480 PRINT"OF 5.3 MEV. THE BEAM STRIKES A 1 MICROMETER THICK GOLD FOIL."
490 PRINT"FIVE ALPHA PARTICLE DETECTORS CAN BE SET UP TO COUNT THE"
500 PRINT"NUMBER OF SCATTERED PARTICLES DURING A COUNTING PERIOD OF"
510 PRINT"100,000 SECONDS (ABOUT 28 HOURS). EACH DETECTOR HAS AN AREA"
520 PRINT"OF 1 SQ CM AND IS 10 CM AWAY FROM THE GOLD FOIL."
530 PRINT"THE ANGULAR LOCATION OF EACH OF THE COUNTERS CAN BE SET"
540 PRINT"BETWEEN THE LIMITS OF 10 DEGREES AND 180 DEGREES BY TYPING"
550 PRINT"A NUMBER BETWEEN 10 AND 180 WHEN THE COMPUTER ASKS: C()=."
560 PRINT"ZERO DEGREES IS FORWARD SCATTERING AND 180 DEGREES IS "
570 PRINT"BACKWARD SCATTERING. TYPE C()=4 TO END THE PROGRAM."
580 REM TARGET PARTICLES PER CM^2
590 LET N=T4*V4*H(3)/W(3)
600 REM DISTANCE OF CLOSEST APPROACH
610 LET D=Z4*Z(3)*B4*D4/E4
620 FOR I=1 TO 5
630 PRINT "C(";I;")=";
640 INPUT G(I)
650 IF G(I)=4 THEN 900
660 IF (G(I)-10)*(180-G(I))>=0 THEN 690
670 PRINT"10<=C(";I;")<=180"
680 GOTO 630
690 NEXT I
700 IF N0>0 THEN 730
710 PRINT TAB(12);"ANGLE   NUMBER SCATTERED    FRACTION SCATTERED"
720 LET N0=1
730 FOR I=1 TO 5
740 LET S=SIN(3.1416*G(I)/360)^4
750 LET P=N*D*D*A4/(16*L4^2*S)
760 REM RAW NUMBER SCATTERED
770 LET C=P*B
780 LET Y=RND(1)
790 LET C1=SQR(C)*COS(6.283*Y)*SQR(-2*LOG(Y))
800 REM RANDOMIZED NUMBER SCATTERED
810 LET D1=C+C1
820 LET D2=INT(D1+.5)*(1+SGN(D1))/2
830 LET J=0
840 LET J=J+1
850 IF INT(D1/10^J)>10 THEN 840
860 LET D5=INT(D1/10^(J-2)+.5)/10^(12-J)
870 PRINT TAB(12);G(I);TAB(20);D2;TAB(40);D5
880 NEXT I
890 GOTO 620
900 END
*U*/**B$$BB$$BB$$BL