;[GALGEN.CTL]
;
; This control file will build the GALAXY configuration program for a
; DECsystem-10 or DECSYSTEM-20.  In the following list of files,
; a (10) indicates the file is required for a TOPS-10 GALGEN and
; a (20) for a TOPS-20 GALGEN.
;
; Sources:	GALGEN.MAC
;
; Input:	GLXLIB.REL	GLXMAC.UNV	MONSYM.UNV(20)	OPRPAR.REL
;		QSRMAC.UNV	UUOSYM.UNV(10)
;
; Output:	GALGEN.EXE
.GOTO TOPS10
@GOTO TOPS20
TOPS10::
.SET WATCH VERSION
.ASSIGN DEC SYS
.ASSIGN DEC REL
.ASSIGN DEC UNV

.DIRECT /CHECK /SLOW GALGEN.MAC

.COMPILE/COMPILE GALGEN.MAC

.R LINK
*GALGEN/SAVE = /LOCALS /SYMSEG:LOW /SEGMENT:LOW -
*GLXLIB/EXCLUDE:GLXINI, GALGEN -
*/GO

.DIRECT /CHECK /SLOW GALGEN.EXE

.PLEASE	GALGEN Assembly Successful
.NOERROR
;.DEASSIGN SYS
.SUBMIT GALAXY = /MODIFY /DEPEND:-1
.GOTO END

%CERR:: .GOTO ERROR
%ERR::  .GOTO ERROR
%TERR:: .GOTO ERROR

ERROR:: .PLEASE Error during GALGEN assembly
.GOTO END
TOPS20::
@DEFINE REL: DEC:
@DEFINE UNV: DEC:

@VDIRECTORY GALGEN.MAC,
@CHECKSUM SEQUENTIAL
@

@COMPILE/COMPILE GALGEN.MAC

@R LINK
*GALGEN/SAVE = /LOCALS /SYMSEG:LOW /SEGMENT:LOW -
*GLXLIB/EXCLUDE:GLXINI, GALGEN -
*/GO

@VDIRECTORY GALGEN.EXE,
@CHECKSUM SEQUENTIAL
@

@PLEASE	GALGEN Assembly Successful
@NOERROR
@MODIFY BATCH GALAXY /DEPEND:-1
@GOTO END

%ERR::  @GOTO ERROR
%TERR:: @GOTO ERROR

ERROR:: @PLEASE Error during GALGEN assembly

END::
%FIN::
;[End of GALGEN.CTL]
