	UNIVER	MAC10
	SUBTTL	MOVX

DEFINE	RHLHA(BIT,AA,BB,CC,DD)<
IFE	<BIT>&777777<
	IFE	<BIT>&777777000000<DD>
	IFN	<BIT>&777777000000<BB>
	>
IFN	<BIT>&777777<
	IFE	<BIT>&777777000000<AA>
	IFN	<BIT>&777777000000<CC>
	>
>

DEFINE	MOVX(AC,BIT)<
RHLHA	<BIT>,<MOVEI AC,<BIT>>,<MOVSI AC,(BIT)>,<MOVE AC,[BIT]>,<SETZ AC,>
>
	SUBTTL	LOAD AND STORE

DEFINE	PNTR(ADDR,MASK)<
POINT	^L<-<<MASK>_^L<MASK>>-1>,ADDR,^L<MASK>+^L<-<<MASK>_^L<MASK>>-1>-1
>

DEFINE	RHLHM(BIT,AA,BB,CC,DD)<
IFE	<BIT>-777777<AA>
IFN	<BIT>-777777<
	IFE	<BIT>-777777000000<BB>
	IFN	<BIT>-777777000000<
		IFE	<BIT>+1<CC>
		IFN	<BIT>+1<DD>
		>
	>
>

DEFINE	LOAD(AC,ADDR,MASK)<
RHLHM	<MASK>,<HRRZ AC,ADDR>,<HLRZ AC,ADDR>,<MOVE AC,ADDR>,<LDB AC,[PNTR ADDR,MASK]>
>

DEFINE	STORE(AC,ADDR,MASK)<
RHLHM	<MASK>,<HRRM AC,ADDR>,<HRLM AC,ADDR>,<MOVEM AC,ADDR>,<DPB AC,[PNTR ADDR,MASK]>
>
	SUBTTL	TEST INSTRUCTIONS

DEFINE	TXXX(AC,BIT,TYPE)<
RHLHA	<BIT>,<TR'TYPE AC,<BIT>>,<TL'TYPE AC,(BIT)>,<TD'TYPE AC,[BIT]>,JFCL
>

DEFINE	TXN(AC,BIT)<JFCL>
DEFINE	TXNN(AC,BIT)<TXXX AC,<BIT>,NN>
DEFINE	TXNE(AC,BIT)<TXXX AC,<BIT>,NE>
DEFINE	TXNA(AC,BIT)<CAIA>

DEFINE	TXO(AC,BIT)<TXXX AC,<BIT>,O>
DEFINE	TXON(AC,BIT)<TXXX AC,<BIT>,ON>
DEFINE	TXOE(AC,BIT)<TXXX AC,<BIT>,OE>
DEFINE	TXOA(AC,BIT)<TXXX AC,<BIT>,OA>

DEFINE	TXZ(AC,BIT)<TXXX AC,<BIT>,Z>
DEFINE	TXZN(AC,BIT)<TXXX AC,<BIT>,ZN>
DEFINE	TXZE(AC,BIT)<TXXX AC,<BIT>,ZE>
DEFINE	TXZA(AC,BIT)<TXXX AC,<BIT>,ZA>

DEFINE	TXC(AC,BIT)<TXXX AC,<BIT>,C>
DEFINE	TXCN(AC,BIT)<TXXX AC,<BIT>,CN>
DEFINE	TXCE(AC,BIT)<TXXX AC,<BIT>,CE>
DEFINE	TXCA(AC,BIT)<TXXX AC,<BIT>,CA>

	END
