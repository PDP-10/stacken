TITLE	RMSINI - GETS AND INITS RMS.EXE FOR TOPS-10 RMS PROGS
SUBTTL	S. COHEN
SEARCH RMSINT

; THIS FILE IS .REQUIRED BY MACRO RMS PROGRAMS.
; BLISS PROGRAMS MUST EXPLICITLY LOAD IT.

RMS$10==:600010				;RESOLVE SYMBOL REFFED IN $verb MACROS
					;(ONLY $verb FOR TOPS-10 MACRO PROGRAMS)

$$RMS::

IFN TOP$10,<
SEARCH	UUOSYM
	SKIPE	SAVE17			;REPEAT CALL?
	POPJ	17,			;YES
	MOVEM	17,SAVE17		;BECAUSE MERGE. CLOBBERS IT
	MOVEI	17,SAVEAC		;SAVE OTHERS NOW
	BLT	17,SAVEAC+16		;DONE
	MOVEI	1,RMS.FS		;LOAD PTR TO RMS FILE SPEC
	MERGE.	1,			;GET IT
	  JRST	INIERR			;OOPS
	MOVE	1,[.PAGCD,,[EXP 2,647,650]];CREATE RMS GLOBALS AREA
	PAGE.	1,			;DO IT
	  JRST	INIERR			;OOPS
	MOVSI	17,SAVEAC		;RESTORE AC'S
	BLT	17,17			;DONE
	POPJ	17,
RMS.FS:
	SIXBIT	/SYS/
	SIXBIT	/RMS/
	SIXBIT	/EXE/
	EXP	0
	EXP	0
	XWD	600,677			;GIVE IT RANGE TO MERGE
SAVEAC:
	BLOCK	17
SAVE17:
	EXP	0
INIERR:
	OUTSTR	[ASCIZ/? Could not initialize RMS
/]
	EXIT	1,
>					;END IFN TOP$10

IFN TOP$20,<
SEARCH	MONSYM
	MOVEI	1,2			;SETUP DUMMY ARGLST
	JSYS	1000+C$MESS		;DO $MESSAGE MANUALLY
	ERJMP	.+2			;...TO SEE IF RMS AROUND
	POPJ	17,			;OK
	MOVEI	1,[ASCIZ/? Could not initialize RMS/]
	PSOUT%				;TELL USER
	HALTF%
>					;END IFN TOP$20

XPUNGE					;USER DOESNT WANT OUR SYMBOLS
END
