	TITLE	DECNET - Make a KDP line into an DECNET line

	SEARCH UUOSYM

DEFINE ERR(text),<
	JRST [	OUTSTR [ASCIZ/?KDPERR	text/]
		EXIT]

>;End of DEFINE ERR

ARGBLK:
FUNCT:	BLOCK 1
KMCNO:	EXP 0
LINE:	BLOCK 1
ARG:	BLOCK 1
	ARGLEN==.-ARGBLK


GO:	JFCL
	RESET
	OUTSTR [ASCIZ/
KDP Number:/]
	INCHWL 1
	SUBI 1,"0"
	CAIL 1,0
	CAILE 1,1
	ERR Illegal DUP number
	MOVEM 1,LINE
	MOVEI 1,.KDLHA		;"HALT" FUNCTION CODE
	MOVEM 1,FUNCT
	SETZM KMCNO		;ONLY KMC 0 SUPPORTED
	MOVE 1,[XWD ARGLEN,ARGBLK]
	KDP. 1,
	 JRST [	CAIE 1,10	;WAS THE ERROR "LINE ALREADY STOPPED"
		CAIN 1,7	; OR LINE NOT RUNNING?
		JRST .+1	;YES, NO COMPLAINT HERE
		ERR KDP. for halting line failed
		]
	MOVEI 1,.KDLSU		;SET USER
	MOVEM 1,FUNCT
	MOVE 1,[SIXBIT/DECNET/]
	MOVEM 1,ARG
	MOVE 1,[XWD ARGLEN,ARGBLK]
	KDP. 1,
	 ERR KDP. for setting line's user failed

	MOVEI 1,.KDLST		;START
	MOVEM 1,FUNCT
	SETZM ARG
	MOVE 1,[XWD ARGLEN,ARGBLK]
	KDP. 1,
	 ERR KDP. for setting line's user failed
	OUTSTR [ASCIZ/[Line set to DECNET user]/]
	EXIT

	END GO
