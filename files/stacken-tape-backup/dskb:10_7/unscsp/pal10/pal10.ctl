;JOB%1(2) TO MAKE PAL10.EXE FROM PAL10.MAC  19 FEB 79
;SUBMIT WITH COMMAND .SUBMIT PAL10/RESTART:1
;
START::	;
.CHKPNT START
;
;REMOVE UNNECESSARY FILES LEFT FROM PREVIOUS RUNS OF THIS .CTL FILE.
.DELETE PAL10.EXE,PAL10.REL
.IF (ERROR)  ;OKAY, THOSE FILES MAY NOT BE THERE.
;
;REQUIRED FILES:  (LATEST RELEASED VERSIONS)
;DEC:	PIP.EXE
;	DIRECT.EXE
;	COMPILE.EXE
;	MACRO.EXE
;	LNK???.EXE
;	LINK.EXE
;	CREF.EXE
;
;SELF:	PAL10.MAC
;
;OUTPUT FILE:
;	PAL10.EXE
;
;OUTPUT LISTINGS:
;	PAL10.LST
;	PAL10.LOG
;
;FILES TO BE SHIPPED:
;	PAL10.CTL
;	PAL10.EXE
;	PAL10.MAC
;
;
;MAKE A DIRECTORY OF REQUIRED FILES (LATEST RELEASED VERSIONS).
.R DIRECT
*TTY:/CHECKSUM=DEC:PIP.EXE,DIRECT.EXE,COMPIL.EXE,MACRO.EXE,LNK???.EXE
*TTY:/CHECKSUM=DEC:LINK.EXE,CREF.EXE
*TTY:/CHECKSUM=PAL10.MAC
;
;
COMPL::	;
.CHKPNT COMPL
;
;
;GET FIELD IMAGE SOFTWARE.
.ASSIGN DEC SYS
.ASSIGN DEC REL
.ASSIGN DEC UNV
;
;MAKE A RECORD OF WHAT IS BEING USED.
.SET WATCH VERSION
;
;
.ERROR %	;FLAG "% ERRORS"
;COMPILE, LOAD, AND SAVE; PRODUCING MAP AND PAL10 FILE.
.R MACRO
*PAL10,PAL10/C=PAL10
.IF (ERROR)  .GOTO ERROR:
;
.R LINK
*PAL10=PAL10/GO
.IF (ERROR)  .GOTO ERROR:
.SAVE DSK:PAL10
.ERROR
;
;TRY IT JUST TO MAKE SURE IT WORKS.
.RUN DSK:PAL10
*/H
.IF (ERROR)  ;OK /H IS ILLEGAL FOR PAL10 ANYWAY
.R DIRECT
*TTY:/CHECKSUM=DSK:PAL10.EXE
;
;PRODUCE SOURCE LISTING AND TELL OPERATOR.
.ASSIGN DSK:LPT		;PUT ".LST" FILE ON DISK.
.R CREF
*PAL10
.PLEASE  PAL10 SUCCESSFUL.	;NOTIFY THE OPERATOR.
.DEASSIGN	;DEASSIGN ALL LOGICAL NAMES.
;
;REMOVE TEMPORARY FILES.
.DELETE PAL10.REL
;
;
ERROR::
%ERR::
%FIN::
;
;END FILE [PAL10.CTL]
