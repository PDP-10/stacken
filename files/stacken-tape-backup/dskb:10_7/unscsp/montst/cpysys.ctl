;CPYSYS.CTL  %013   CREATE AND RUN CPYSYS.SCP WHICH COPIES ALL SHR,
;SAV AND EXE FILES FROM SYS TO TEST HIGH VOLUME OF DISK I/O, SIMULTANEOUS
;READING OF FILE BY MANY JOBS, AND EXCEEDING FCFS QUOTAS.
;4 AUG 77    SM LIBMAN
;
.GOTO SKIP
SCP::
!;; CPYSYS.SCP - SCRIPT TO COPY FILES FROM SYS THEN DELETE THEM - %013
!;;FOR 603 MONITOR
!;;T. HASTINGS/TH/PFC/PW/SML  4 AUG 77
!IORFS;;RUN AS FAST AS POSSIBLE
LOGIN
4,777#
!Q
TEST#
!XIN
!;;SET TO RUN IN V.M.
!Q
R SETVM
SET WATCH VERSION
ZERO DSK:
SET TIME 20
!Q
COPY DSK:=DSKA:[1,4]*.SHR,*.SAV,*.EXE
SET TIME 20
!Q
COPY DSK:=DSKB:[1,4]*.SHR,*.SAV,*.EXE
SET TIME 20
!Q
COPY DSK:=DSKC:[1,4]*.SHR,*.SAV,*.EXE
^C
SET TIME 0
!;; NOW CHECK IT ALL TO SEE IF IT'S OK
DIR NUL:=/CHECKSUM
DEL *.*
KJOB/D/D
!XOUT
SKIP::
;RUN VIRTUALLY
.R SETVM
.IF (ERROR)   ;IGNORE
.R TECO
=ERCPYSYS.CTL
*_SCP::
=0,.K
=EWCPYSYS.SCP
=NSKIP::0L
=.,ZKPWEF
;
;END OF SCRIPT FILE; NOW RUN IT, 7 JOBS
;
.R SCRIPT
*CPYSYS
.CON
*7
*
*CPYSYS
*CPYSYS
*
*
*
*Y
*N
*Y
.IF (ERROR) .GOTO B
.PLEASE CPYSYS SUCCESSFUL
.DELETE CPYSYS.WCH
.GOTO A
B::
.PLEASE ERROR RUNNING CPYSYS
.QUEUE CPYSYS.WCH/DISPOSE:DELETE
A::
%FIN:
.NOERROR
.DELETE CPYSYS.SCP
