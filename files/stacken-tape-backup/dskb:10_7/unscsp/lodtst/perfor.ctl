;JOB%(1) PERFOR.CTL [CONTROL FILE TO BUILD SCHEDULER PERFORMANCE PACKAGE]
; FEBRUARY 1977
;SUBMIT WITH COMMAND .SUBMIT PERFOR/RESTART:1
;
START::
.CHKPNT START
;
;REMOVE UNNECESSARY FILES LEFT FROM PREVIOUS RUNS OF THIS .CTL FILE.
.DELETE LINES.EXE,LINES.REL,LNSRPT.EXE,LNSRPT.REL,EXIST.REL,SIMP1.EXE
.IF (ERROR)
.DELETE SIMP1.REL,GRAPH1.REL,GRAPH1.EXE
.IF (ERROR)
;REQUIRED FILES (LATEST RELEASED VERSIONS)
;DEC:	PIP.EXE
;	DIRECT.EXE
;	COMPILE.EXE
;	MACRO.EXE
;	LNK???.EXE
;	LINK.EXE
;	CREF.EXE
;	FOROTS.EXE
;	FORTRA.EXE
;	FORTB.EXE
;	FORTC.EXE
;	FORTD.EXE
;	FORTE.EXE
;	FORTF.EXE
;	FORTG.EXE
;OUTPUT FILES
;	LINES.EXE
;	LNSRPT.EXE
;	SIMP1.EXE
;	GRAPH1.EXE
;OUTPUT LISTINGS
;	LINES.LST
;	LNSRPT.LST
;	SIMP1.LST
;	GRAPH1.LST
;FILES TO BE SHIPPED
;	PERFOR.CTL
;	LINES.MAC
;	LINES.EXE
;	LNSRPT.FOR
;	EXIST.MAC
;	GRAPH1.FOR
;	SIMP1.FOR
;
;MAKE A DIRECTORY OF ALL REQUIRED FILES (LATEST RELEASED VERSIONS)
.R DIRECT
*TTY:/CHECKSUM=DEC:PIP.EXE,DIRECT.EXE,COMPILE.EXE,MACRO.EXE,LNK???.EXE,-
*LINK.EXE,CREF.EXE,FOROTS.EXE,FORTRA.EXE,FORTB.EXE,FORTC.EXE,FORTF.EXE,-
*FORTE.EXE,FORTF.EXE,FORTG.EXE
.DIRECT /CHECKSUM LINES.MAC,LNSRPT.FOR,EXIST.MAC,SIMP1.FOR,GRAPH1.FOR
;
;
COMPL::
.CHKPNT COMPL
;
;
;GET FIELD IMAGE SOFTWARE
.ASSIGN DEC SYS
;
;MAKE A RECORD OF WHAT IS BEING USED
.SET WATCH VERSION
.IF (ERROR) ;IF TINY MONITOR
;
.ERROR %
.ASSIGN DSK LPT
;
.LOAD LINES.MAC/CREF
.NSAVE LINES
.VERSION
.IF(ERROR).E 137
.CREF
;
.LOAD /CREF LNSRPT.FOR(KA10)
.NSAVE LNSRPT
.VERSION
.IF(ERROR).E 137
.CREF
;
.LOAD /CREF GRAPH1.FOR(KA10)
.NSAVE GRAPH1
.VERSION
.IF(ERROR).E 137
.CREF
;
.LOAD /CREF SIMP1.FOR(KA10),EXIST
.NSAVE SIMP1
.VERSION
.IF(ERROR).E 137
.CREF
;DELETE INTERMEDIATE FILES
;
.DELETE LINES.REL,LNSRPT.REL,EXIST.REL,SIMP1.REL,GRAPH1.REL
;
.DIRECT /CHECKSUM LINES.EXE,LNSRPT.EXE,SIMP1.EXE,GRAPH1.EXE
.GOTO DONE
;
;IF THIS JOB FAILED, MAKE A ".ERR" FILE ON THE DISK TO LET YOU
; KNOW THAT IT FAILED
ERROR::
%ERR::
%FIN::
;
.COPY PERFOR.ERR=NUL:PERFOR.ERR
.PLEASE PERFORM.CTL FAILED.
DONE::
;END FILE [PERFOR.CTL]
