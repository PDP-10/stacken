TITLE	ACTRCD - SYMBOLS AND MACROS NEEDED BY TOPS10 ACCOUNTING DAEMON - V1(14)

SUBTTL	B.A.HUIZENGA/BAH/JMF - 29-AUG-85

	SEARCH	ACTSYM

	SALL



;COPYRIGHT (c) DIGITAL EQUIPMENT CORPORATION 1980,1981,1985,1986.
;ALL RIGHTS RESERVED.
;
;
;THIS SOFTWARE IS FURNISHED UNDER A LICENSE AND MAY BE USED AND COPIED
;ONLY  IN  ACCORDANCE  WITH  THE  TERMS  OF  SUCH LICENSE AND WITH THE
;INCLUSION OF THE ABOVE COPYRIGHT NOTICE.  THIS SOFTWARE OR ANY  OTHER
;COPIES THEREOF MAY NOT BE PROVIDED OR OTHERWISE MADE AVAILABLE TO ANY
;OTHER PERSON.  NO TITLE TO AND OWNERSHIP OF THE  SOFTWARE  IS  HEREBY
;TRANSFERRED.
;
;THE INFORMATION IN THIS SOFTWARE IS SUBJECT TO CHANGE WITHOUT  NOTICE
;AND  SHOULD  NOT  BE  CONSTRUED  AS A COMMITMENT BY DIGITAL EQUIPMENT
;CORPORATION.
;
;DIGITAL ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY  OF  ITS
;SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DIGITAL.


	AR.VER==1		;VERSION NUMBER
	AR.EDT==14		;EDIT NUMBER
	AR.WHO==0		;WHO EDITED LAST
	AR.MIN==0		;MINOR VERSION NUMBER
	SUBTTL	EDIT HISTORY

;1)	WRITE MACROS TO GENERATE TABLES (ENTRYS BY RECORDS
;	BY DEFUS DATA NUMBERS) SO THAT THE ACCOUNT DAEMON CAN PREFILL AND PUT
;	USER-PROVIDED DATA INTO A USAGE ENTRY.  ENTRYS ARE DEFINED BY A LIST
;	OF RECORDS; RECORDS ARE DEFINED BY A LIST OF DEFUS NUMBERS. DEFUS
;	NUMBERS ARE DEFINED IN ACTSYM.MAC AND ARE USED BY BOTH TOPS10 AND
;	TOPS20 OPERATING SYSTEMS.

;2)	DEFINE ALL SUPPORTED ENTRYS AND RECORDS WITH THE PROPER
;	REVISION NUMBERS.
;3)	USAGE header entry record definition is incorrect.
;4)	REDEFINE RECORD DSU (DISK SPINDLE USAGE) TO USE DEFUS FOR SID (PACK ID)
;5)	"DST" CHANGED TO "DSQ" IN RECORD DTA (DECTAPE MOUNT)
;6)	ADD "UPF,FPF,TMA" TO DISK USAGE ENTRY
;7)	"STR" CHANGED TO "DFS" IN RECORD DUD (DISK USAGE)
;10)	ADD "DVN" TO MAG/DECTAPE ENTRIES
;11)	BUILD SECOND TABLE FOR USER DEFINED ENTRY TYPES (THOSE FROM 5000-9999)
;12)	CHANGE MAGTAPE REVISION NUMBER TO 3 PER EDIT 34 OF ACTSYM
;13)	"BSN" APPEARS TWICE IN THE SESSION ENTRY, CHANGE ONE OF THEM TO "RIN"
;14)	ADD DIU-10 RECORD DEFINITIONS
	SUBTTL	USAGE FILE ENTRY AND RECORD DEFINITIONS FOR THE ACCOUNT DAEMON

	DEFINE	DEFUS(NAM,VAL,TYPE,LENGTH) <
		NAM'TYP==TYPE
		NAM'LEN==^D'LENGTH
	>
	USLIST

	DEFINE	ENLIST<
	XLIST

	ENTRY	(RST,<UEH,RES>)		;0001 - SYSTEM RESTART
	ENTRY	(SES,<UEH,SE1,SE2,UID>)	;0002 - SESSION
	ENTRY	(ISE,<UEH,SE1,SE2,UID>)	;0003 - SESSION (INCOMPLETE)
	ENTRY	(UFE,<UEH,UFH>)		;0004 - USAGE FILE HEADER
	ENTRY	(DTC,<UEH,DTM>)		;0005 - DATE/TIME CHANGE
	ENTRY	(BAT,<UEH,BPR,UID>)	;0006 - BATCH
	ENTRY	(INP,<UEH,ISR,UID>)	;0007 - INPUT SPOOLER
	ENTRY	(OUP,<UEH,OSR,UID>)	;0008 - OUTPUT SPOOLER
	ENTRY	(DUE,<UEH,DUD,DUA>)	;0009 - DISK SPACE USAGE
	ENTRY	(DSE,<UEH,DSU>)		;0010 - SYSTEM SPINDLE UTILIZATION
	ENTRY	(FSE,<UEH,FSM,UID>)	;0011 - STRUCTURE MOUNT
	ENTRY	(MME,<UEH,MTM,UID>)	;0012 - MAGTAPE MOUNT
	ENTRY	(DME,<UEH,DTA,UID>)	;0013 - DECTAPE MOUNT
	ENTRY	(DFE,<UEH,DFC,UID>)	;0014 - FILE COMMAND (DECTAPE)
	ENTRY	(RET,<UEH>)		;0015 - File retrieved
	ENTRY	(ARC,<UEH>)		;0016 - File archived
	ENTRY	(MIG,<UEH>)		;0017 - File migrated
	ENTRY	(COL,<UEH>)		;0018 - File collected
	ENTRY	(DIU,<UEH,DI1>)	;0019 - DIU request

	LIST
	SALL
	>
	DEFINE	RCLIST<
	XLIST

	RECORD	(UEH,1,1,<JNO,TAD,TRM,LNO,PNM,PVR,AMV,NOD>)
	RECORD	(UID,1,1,<PPN,NM1,NM3>)
	RECORD	(RES,1,1,<SNM,MVR,MBD,MUP,CPN,CP0,CP1,CP2,CP3,CP4,CP5,LCK>)
	RECORD	(UFH,1,1,<SNM,MVR,MBD,MUP,CPN,CP0,CP1,CP2,CP3,CP4,CP5,LCK>)
	RECORD	(SE1,2,1,<ACT,RTM,SST,JTY,BJN,BSN,COM,CCT,RIN>)
	RECORD	(SE2,1,1,<DKR,DKW,CTI,VTI,EBX,MBX,MCL,MCM,SCL,TYI,TYO,TYW,RQQ>)
	RECORD	(DTM,1,1,<OFD,OFS,ODT>)
	RECORD	(BPR,1,1,<BAC,BRN,BCT,BDR,BDW,BJB,BSQ,BDT,BET,BST,BDS,BTX,BPR,URE,UAC,UCE,UCH,RIN>)
	RECORD	(ISR,2,1,<IAC,IRN,ICT,IDR,IDW,IJN,IQN,IPD,ISN,ICR,ICD,IDS,ITX,IPR,IRI,ICN>)
	RECORD	(OSR,2,1,<OAC,SRT,SCI,SDR,SDW,JNM,QNM,SDV,SSN,SUN,SNF,CRT,SCD,FRM,DSP,TXT,PRI,ORI,OCN>)
	RECORD	(DUD,2,1,<NRF,TAL,TUS,TNF,DFS,PPN,B27,STP,KTP,DTP,LIQ,LOQ,LLG,LAT,EXP,FON,UPF,FPF,TMA>)
	RECORD	(DUA,1,1,<DAC,PPN,B27,ALC,USG,FIL,DFS,DFT,DKT,DDT>)
	RECORD	(DSU,1,1,<SFS,SFT,SCT,SDT,SID,B06,SDU,SNP,SMN,DTF,DCC>)
	RECORD	(FSM,1,1,<FMA,SSI,FST,TNP,FCT,FDT,FDS,FOT,FCD,FSD,SRV,MCT,DCT,ATP,FCO>)
	RECORD	(MTM,3,1,<MAC,VID,VSN,MRF,MWF,MDS,MTX,MCD,MSD,MVD,MCO,MLT,MLS,MRD,MWR,FSI,SRE,SWE,HRE,HWE,MCN,DVN>)
	RECORD	(DTA,2,1,<DAN,DVI,DRI,DTR,DTW,DDS,DTX,DCE,DSQ,DSS,DCN,DVN>)
	RECORD	(DFC,1,1,<FAS,FMR,FCI,FDR,FDW,FCM,FNF,FDP,FTX,FCQ,FSH,FVD,FCE>)
	RECORD	(DI1,1,1,<ACT,DIZ,DIP,DIE,DIT,DIX,DIB,DIW,DIN,DII,DIS,DID,DIC,DIJ,DIM,DIQ>)

	LIST
	SALL
	>
	SUBTTL	MACROS THAT DEFINE ENTRYS AND THEIR RECORDS

	DEFINE	RECORD(A,B,C,D)<
	A'LEN==^D21
	.US'A::	<A'CNT>B17+<B>B26+<C>B35
	A'CNT==0
	IRP	D,<<D'TYP>B11+<D'LEN>B20+.US'D
		A'LEN
		A'LEN==A'LEN+D'LEN
		A'CNT==A'CNT+1>
	A'LEN==A'LEN-1
	>

	DEFINE	ENTRY(A,B)<
	.US'A::	A'CNT
	A'CNT==0
	IRP	B,<B'LEN,,.US'B
		A'CNT==A'CNT+1>
	>

	RCLIST
	ENLIST

	DEFINE	ENTRY(A)<
		.US'A
	>
ENTRYS::
	ENLIST
ENTRYL==:.-ENTRYS
SUBTTL	NOW FOR THE USER DEFINED ENTRY TYPES 5000-9999

	DEFINE	ENLIST<
	XLIST

;;Insert entry definitions after these comments.  The entry number will
;;	be assigned sequentially starting with 5000.  This table is order
;;	dependent.  See the above section where Digital entry types are
;;	defined for format of the ENTRY record. Also see the sample programs
;;	distributed for instructions on inserting the entry and record
;;	definitions they need for correct operation.

	LIST
	SALL
	>

	DEFINE	RCLIST<
	XLIST

;;Individual record definitions are inserted after these comments.  There
;;	is no particular order to the record definitions as they are referenced
;;	in the ENTRY definition.

	LIST
	SALL
	>

	DEFINE	ENTRY(A,B)<
	.US'A::	A'CNT
	A'CNT==0
	IRP	B,<B'LEN,,.US'B
		A'CNT==A'CNT+1>
	>

	RCLIST
	ENLIST

	DEFINE	ENTRY(A)<
		.US'A
	>
ENTRYU::
	ENLIST
ENTRUL==:.-ENTRYU

	END
